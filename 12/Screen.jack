// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

class Screen {
	//The Actual Screen
    static Array screen;
	
	//Current Color, compared against black|White
    static boolean currColor;
	static boolean white;
    static boolean black;
	
	
    static int whitePx;
    static int blackPx;
    
    /** Initializes the Screen. */
    function void init() {
        let screen = 16384;
        let white = false;
        let black = true;
        let whitePx = 0;
        let blackPx = 1;
        let currColor = black;
        return;
    }

    /** Zeroes out screen */
    function void clearScreen() {
        var int i;
        let i = 0;
        while(i < 8192) {
            let screen[i] = white;
        }
        return;
    }

    /** Sets the color to be used in further draw commands */
    function void setColor(boolean b) {
        let currColor = b;
        return;
    }

    /** Draws the (x, y) pixel. */
    function void drawPixel(int x, int y) {
        var int addr;
        var int pow;

        let addr = (y*32) + (x/16);
        let pow = Math.pow(2, (x & 15));

        if (currColor) {
            let screen[addr] = screen[addr] | pow;
        } else {
            let screen[addr] = screen[addr] & ~pow;
        }
        return;
    }

    /** Draws a line from (x1, y1) to (x2, y2). */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int temp;
        
        // Make sure x1 <= x2
        if(x1 > x2) {
            let temp = x1;
            let x1 = x2;
            let x2 = temp;
            let temp = y1;
            let y1 = y2;
            let y2 = temp;
        }
        
        if((x2 - x1) = 0) {
            do Screen.drawVerticalLine(x1, y1, y2);
        } else { 
			if((y2 - y1) = 0) {
				do Screen.drawHorizontalLine(x1, x2, y1);
			} else {
				do Screen.drawDiagonalLine(x1, y1, x2, y2, (x2 - x1), (y2 - y1));
			}
		}
        return;
    }
    
    function void drawDiagonalLine(int x1, int y1, int x2, int y2, int dx, int dy) {
        var int a, b;
        var int adyMinusbdx;
        var int y_incr;

        let a = 0;
        let b = 0;
        let adyMinusbdx = 0;
        
        if(dy < 0) {
            let y_incr = -1;
        }
        else {
            let y_incr = 1;
        }

        while(~(a > dx) & (((y_incr = 1) & ~(b > dy)) | ((y_incr = -1) & ~(b < dy)))) {
            do Screen.drawPixel(x1+a, y1+b);
            if(adyMinusbdx < 0) {
                let a = a + 1;
                let adyMinusbdx = adyMinusbdx + (dy*y_incr);
            }
            else {
                let b = b + y_incr;
                let adyMinusbdx = adyMinusbdx - dx;
            }
        }
        return;
    }

    function void drawVerticalLine( int x, int y1, int y2 ) {
        var int temp;
        
        // Make sure y1 <= y2
        if(y1 > y2) {
            let temp = y1;
            let y1 = y2;
            let y2 = temp;
        }
        
        while(~(y1 > y2)) {
            do Screen.drawPixel(x, y1);
            let y1 = y1 + 1;
        }
        return;
    }
    
    function void drawHorizontalLine(int x1, int x2, int y) {
        var int start_addr, end_addr;
        var int x1mod16, x2mod16;
        
        let x1mod16 = x1 & 15;
        let x2mod16 = x2 & 15;
        let start_addr = (y*32) + (x1/16);
        let end_addr = (y*32) + (x2/16) + (x2mod16=0);

        if(start_addr = end_addr) {   // line starts and ends within the same screen address
            do Screen.lazyHorizLine(x1, x2, y);
        }
        else { 
            if(~(x1mod16 = 0)) {      // Draw the short start of the line
                let start_addr = start_addr + 1;
                do Screen.lazyHorizLine(x1, x1+16-x1mod16, y);
            }
            if(~(x2mod16 = 0)) {      // Draw the short end of the line
                let end_addr = end_addr - 1;
                do Screen.lazyHorizLine(x2-x2mod16, x2, y);
            }
            while(~(start_addr > end_addr)) {     // Draw the middle of the line
                let screen[start_addr] = currColor;
                let start_addr = start_addr + 1;
            }
        }
        
        return;
    }
    
    function void lazyHorizLine(int x1, int x2, int y) {
        while(~(x1 > x2)) {
            do Screen.drawPixel(x1, y);
            let x1 = x1 + 1;
        }
        return;
    }
    
    /** Draws a filled rectangle where the top left corner
     *  is (x1, y1) and the bottom right corner is (x2, y2). */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int y;
        let y = y1;
        while(~(y > y2)) {
            do Screen.drawHorizontalLine(x1, x2, y);
            let y = y + 1;
        }
        return;
    }

    /** Draws a filled circle of radius r around (cx, cy). */
    function void drawCircle(int cx, int cy, int r) {
        var int dx, dy;
        var int r_squared;
        
        let dy = -r;
        let r_squared = r*r;
        while(~(dy > r)) {
            let dx = Math.sqrt(r_squared-(dy*dy));
            do Screen.drawHorizontalLine(cx-dx, cx+dx, cy+dy);
            let dy = dy + 1;
        }
        return;
    }
}
