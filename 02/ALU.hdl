// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs --
        zx, // zero the x input? --
        nx, // negate the x input? --
        zy, // zero the y input? --
        ny, // negate the y input? --
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
	//Opposite Num
	Not16(in=x,out=NOTx);
	Not16(in=y,out=NOTy);
	
	//If Invert; if Zero
	Mux4Way16(a=x,b=false,c=NOTx,d=true,sel[0]=zx,sel[1]=nx,out=FX);
    Mux4Way16(a=y,b=false,c=NOTy,d=true,sel[0]=zy,sel[1]=ny,out=FY);
	
	//Do Maths
	Add16(a=FX,b=FY,out=ADDFXY);
	And16(a=FX,b=FY,out=ANDFXY);
	
	//"F" Switch
	Mux16(a=ANDFXY,b=ADDFXY,sel=f,out=fFXY);
	
	//Negate? Output
	Not16(in=fFXY,out=NOTf);
	Mux16(a=fFXY,b=NOTf,sel=no,out=out,out[0..7]=OUT07,out[8..15]=OUT815,out[15]=ng);
	
	//Or 16 Way isnt a thing (by default)
	Or8Way(in=OUT07,out=OR1);
    Or8Way(in=OUT815,out=OR2);
    Or(a=OR1,b=OR2,out=OR);
	Not(in=OR,out=zr);
	
}